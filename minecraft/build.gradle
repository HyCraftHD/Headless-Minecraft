buildscript {
    repositories {
	    mavenCentral()
	    maven {
	    	url "https://files.minecraftforge.net/maven"
	    }
		maven {
			url "https://repo.u-team.info"
    	}
	}

    dependencies {
    	classpath "net.hycrafthd:minecraft_downloader:1.0.0-SNAPSHOT"
        classpath "net.minecraftforge:srgutils:0.4.1"
        classpath "net.md-5:SpecialSource:1.9.0"
    }
}

plugins {
   id "java-library"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}
}

jar {
}

configurations {
	minecraft
	compile.extendsFrom(minecraft)
}

// Setup project

// Imports
import net.minecraftforge.srgutils.IMappingFile
import net.minecraftforge.srgutils.IMappingFile.Format

// Directories
def minecraft_files = file("$buildDir/minecraft_files")
def minecraft_downloader = file("$buildDir/minecraft_downloader")
def mappings = file("$buildDir/mappings")
def mapped = file("$buildDir/mapped")

// Files
def official_mappings_file = project.file("${minecraft_files}/${minecraft_version}-client.txt")
def obf_to_mapping = project.file("${mappings}/obf_to_mapping.srg")
def mapping_to_obf = project.file("${mappings}/mapping_to_obf.srg")
def minecraftJar = project.file("${minecraft_files}/${minecraft_version}-client.jar")
def mappedMinecraftJar = project.file("${mapped}/mapped-minecraft.jar")
def libraryList = project.file("${minecraft_files}/libraries.txt")
def accessChange = project.file("$projectDir/src/main/resources/access_change.at")

// Create directories
minecraft_files.mkdirs()
minecraft_downloader.mkdirs()
mappings.mkdirs()
mapped.mkdirs()

// Download minecraft
javaexec { java ->
	classpath buildscript.configurations.classpath
	main "net.hycrafthd.minecraft_downloader.Main"
	args "--version", minecraft_version, "--output", minecraft_files, "--skipAssets", "--extraInformation", "--libraryList", libraryList

	workingDir minecraft_downloader
}.rethrowFailure().assertNormalExitValue()
	
// Generate tsrg files from official mappings
def mappingFile = IMappingFile.load(official_mappings_file)
mappingFile.write(obf_to_mapping.toPath(), Format.TSRG, true)
mappingFile.write(mapping_to_obf.toPath(), Format.TSRG, false)
	
// Remap the minecraft jar
javaexec { java ->
	classpath buildscript.configurations.classpath
	main "net.md_5.specialsource.SpecialSource"
	args "--in-jar", minecraftJar, "--out-jar", mappedMinecraftJar, "--srg-in", obf_to_mapping, "--access-transformer", accessChange
	workingDir mapped
}.rethrowFailure().assertNormalExitValue()

dependencies {
	minecraft files(mappedMinecraftJar)
	minecraft files(libraryList.readLines())
}