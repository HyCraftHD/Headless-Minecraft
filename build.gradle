buildscript {
	repositories {
		maven {
			url = "https://maven.minecraftforge.net"
		}
		maven {
			url = "https://maven.parchmentmc.org"
		}
		mavenCentral()
	}
	dependencies {
		classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+", changing: true
		classpath group: "org.parchmentmc", name: "librarian", version: "1.+", changing: true
	}
}

apply plugin: "eclipse"

group = "net.hycrafthd"
//archivesBaseName = "headless-minecraft"
version = "1.18.1-0.0.1"

subprojects {
	apply plugin: "eclipse"
	apply plugin: "java"

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}
}

ext {
	//MAPPING_CHANNEL = "parchment"
	//MAPPING_VERSION = "2022.01.23-1.18.1"
	MAPPING_CHANNEL = "official"
	MAPPING_VERSION = "1.18.1"
	MC_VERSION = "1.18.1"
	MCP_VERSION = "20211210.034407"
	SPI_VERSION = "4.0.10"
	
	FART = "net.minecraftforge:ForgeAutoRenamingTool:0.1.17:all"
}

println(" Java: " + System.getProperty("java.version") +
		" JVM: " + System.getProperty("java.vm.version") + "(" + System.getProperty("java.vendor") + ")" +
		" Arch: " + System.getProperty("os.arch"))


project(":mcp") {
	apply plugin: "net.minecraftforge.gradle.mcp"
	mcp {
		config MC_VERSION + "-" + MCP_VERSION
		pipeline = "joined"
	}
}

project(":clean") {
	evaluationDependsOn(":mcp")
	
	apply plugin: "net.minecraftforge.gradle.patcher"
	
	dependencies {
		implementation "net.minecraftforge:forgespi:" + SPI_VERSION
	}
	
	patcher {
		parent = project(":mcp")
		mcVersion = MC_VERSION
		patchedSrc = file("src/main/java")
		
		mappings channel: MAPPING_CHANNEL, version: MAPPING_VERSION
	}
}

project(":minecraft") {
	evaluationDependsOn(":clean")
	
	apply plugin: "net.minecraftforge.gradle.patcher"
	
	sourceSets {
        main {
            java {
                srcDirs = [
                    "$rootDir/src/main/java",
                ]
            }
            resources {
                srcDirs = [
                    "$rootDir/src/main/resources",
                    "$rootDir/src/generated/resources",
                ]
            }
        }
    }
	
	dependencies {
		implementation "net.minecraftforge:forgespi:" + SPI_VERSION
	}
	
	patcher {
		parent = project(":clean")
		patches = file("$rootDir/patches")
		patchedSrc = file("src/main/java")
		srgPatches = true
		
		runs {
			minecraft_client {
				client true
				taskName "minecraft_client"
				ideaModule "${rootProject.name}.${project.name}.main"
				
				main "net.minecraft.client.main.Main"
				workingDirectory project.file("run")
				
				args "--gameDir", "."
				args "--version", MC_VERSION
				args "--assetsDir", downloadAssets.output
				args "--assetIndex", "{asset_index}"
				args "--accessToken", "0"
			}
			
			minecraft_server {
				client false
				taskName "minecraft_server"
				ideaModule "${rootProject.name}.${project.name}.main"
				
				main "net.minecraft.server.Main"
				workingDirectory project.file("run")
			}
		}
	}
	
	genPatches {
		autoHeader true
		lineEnding = "\n"
	}
	
	reobfJar {
        tool = FART
        args = ['--input', '{input}', '--output', '{output}', '--map', '{srg}']
    }
}

task setup {
	group = "setup"
	description = "Setups the dev workspace"
	
	dependsOn ":clean:extractMapped"
	dependsOn ":minecraft:extractMapped"
}